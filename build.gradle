buildscript {
    ext {
        springBootVer = '3.4.0'
        springDepManVer = '1.1.6'
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'io.spring.dependency-management' version "${springDepManVer}"
    id 'org.springframework.boot' version "${springBootVer}"
    id 'com.github.dawnwords.jacoco.badge' version '0.2.4'
}

group = 'io.github.untalsanders.resume'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_22
    targetCompatibility = JavaVersion.VERSION_22
}

repositories {
    mavenCentral()
}

ext {
    postgresqlVersion = '42.7.3'
    lombokVersion = '1.18.34'
    mapStructVersion = '1.6.3'
}

dependencies {
    // Production
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "org.mapstruct:mapstruct:${mapStructVersion}"

    // Runtime
    runtimeOnly 'com.h2database:h2'
    runtimeOnly "org.postgresql:postgresql:${postgresqlVersion}"

    // Compile
    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    // Annotations Processor
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"

    // Test
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    // Development
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    finalizedBy jacocoTestCoverageVerification

    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            enabled = true
            limit {
                minimum = 0.80
            }
        }
    }
}

jacocoBadgeGenSetting {
    readmePath "$projectDir/README.md"
    limit = ['instruction': 0, 'complexity': 0, 'line': 0, 'method': 0, 'class': 0]
}
