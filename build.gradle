buildscript {
    ext {
        springBootVer = '3.2.0'
        springDepManVer = '1.1.4'
    }
}

plugins {
    id 'application'
    id 'jacoco'
    id 'io.spring.dependency-management' version "${springDepManVer}"
    id 'org.springframework.boot' version "${springBootVer}"
    id 'com.github.dawnwords.jacoco.badge' version '0.2.4'
}

group = 'io.github.untalsanders.resume'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

dependencies {
    // Development
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Production
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'

    // Production (Runtime)
    runtimeOnly 'com.h2database:h2'

    // Annotations Processor
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // Test
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

application {
    mainClassName = 'io.github.untalsanders.resume.ResumeApplication'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    finalizedBy jacocoTestCoverageVerification

    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            enabled = true
            limit {
                minimum = 0.80
            }
        }
    }
}

jacocoBadgeGenSetting {
    readmePath "$projectDir/README.md"
    limit = ['instruction': 0, 'complexity': 0, 'line': 0, 'method': 0, 'class': 0]
}
